
&НаСервере
Процедура СформироватьНаСервере()
	
	Если Не ЗначениеЗаполнено(Период.ДатаНачала) или Не ЗначениеЗаполнено(Период.ДатаОкончания) ИЛИ Не ЗначениеЗаполнено(Город) Тогда
		Сообщить("Не все параметры заполнены");
		Возврат
	КонецЕсли;
	
	ТабДок.Очистить();
	Запрос = Новый запрос(
	"ВЫБРАТЬ
	|	Филиалы.Ссылка КАК Ссылка,
	|	Филиалы.Наименование КАК Наименование,
	|	Филиалы.Город.Наименование КАК ГородНаименование
	|ПОМЕСТИТЬ ВТ_Филиалы
	|ИЗ
	|	Справочник.Филиалы КАК Филиалы
	|ГДЕ
	|	Филиалы.Город = &Город
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Филиалы.ГородНаименование КАК Город,
	|	ВТ_Филиалы.Наименование КАК Филиал,
	|	ПродажаТовараОбороты.Регистратор КАК Документ,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПродажаТовараОбороты.Номенклатура) КАК КолНоменклатуры,
	|	СУММА(ПродажаТовараОбороты.СуммаПродажиОборот) КАК СуммаПродажи
	|ИЗ
	|	ВТ_Филиалы КАК ВТ_Филиалы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПродажаТовара.Обороты(&ДатаС, &ДатаДо, Регистратор, ) КАК ПродажаТовараОбороты
	|		ПО ВТ_Филиалы.Ссылка = ПродажаТовараОбороты.Филиал
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Филиалы.ГородНаименование,
	|	ВТ_Филиалы.Наименование,
	|	ПродажаТовараОбороты.Регистратор
	|
	|УПОРЯДОЧИТЬ ПО
	|	Город,
	|	Филиал");
	
	Запрос.УстановитьПараметр("Город",Город);
	Запрос.УстановитьПараметр("ДатаС",Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаДо",Период.ДатаОкончания);

	ВыборкаДанных = Запрос.Выполнить().Выбрать();
	
	ОбъектОтчет = РеквизитФормыВЗначение("Отчет");
	Макет = ОбъектОтчет.ПолучитьМакет("Макет");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("ОбластьСтрока");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ТабДок.Вывести(ОбластьШапка);
	
	СуммаИтог = 0;
	
	Пока ВыборкаДанных.Следующий() Цикл
		ОбластьСтрока.Параметры.Заполнить(ВыборкаДанных);
		СуммаИтог = СуммаИтог + ВыборкаДанных.СуммаПродажи;
		ТабДок.Вывести(ОбластьСтрока);
	КонецЦикла;
	
	ОбластьПодвал.Параметры.СуммаИтог = СуммаИтог;
	ТабДок.Вывести(ОбластьПодвал); 	
	
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьНаСервере();
КонецПроцедуры
