
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПриходнаяНакладная") Тогда 
		
		Автор = ДанныеЗаполнения.Автор;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		ФилиалОтправитель = ДанныеЗаполнения.Филиал;
		
		ЗапросОстатки = Новый Запрос("ВЫБРАТЬ
		|	ОстаткиТовараОстатки.Номенклатура КАК Номенклатура,
		|	СУММА(ОстаткиТовараОстатки.КоличествоОстаток) КАК Количество
		|ИЗ
		|	РегистрНакопления.ОстаткиТовара.Остатки(, Филиал = &Филиал) КАК ОстаткиТовараОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ОстаткиТовараОстатки.Номенклатура");
		
		ЗапросОстатки.УстановитьПараметр("Филиал", ДанныеЗаполнения.Филиал);
		ТаблицаОстатки = ЗапросОстатки.Выполнить().Выгрузить();
		
		Для Каждого ТекСтрокаНоменклатура Из ДанныеЗаполнения.Номенклатура Цикл
			Если ЗначениеЗаполнено(ТекСтрокаНоменклатура.Номенклатура) Тогда
				НайденнаяСтрока = ТаблицаОстатки.Найти(ТекСтрокаНоменклатура.Номенклатура,"Номенклатура");
				
				Если НайденнаяСтрока = Неопределено Тогда
					Сообщить("Товар " + ТекСтрокаНоменклатура.Номенклатура + " на остатках отсутствует")
				Иначе
					НоваяСтрока = Состав.Добавить(); 
					НоваяСтрока.Номенклатура = ТекСтрокаНоменклатура.Номенклатура;
					НоваяСтрока.Цена = ТекСтрокаНоменклатура.Цена;
					
					Если НайденнаяСтрока.Количество < ТекСтрокаНоменклатура.Количество Тогда
						Сообщить(СтрШаблон("Товара %1 на остатках было всего %2, количество было изменено", ТекСтрокаНоменклатура.Номенклатура, НайденнаяСтрока.Количество)); 
						НоваяСтрока.Количество = НайденнаяСтрока.Количество;
					Иначе
						НоваяСтрока.Количество = ТекСтрокаНоменклатура.Количество;
					КонецЕсли;
					НайденнаяСтрока.Количество = НайденнаяСтрока.Количество - ТекСтрокаНоменклатура.Количество;
					
					НоваяСтрока.Сумма = ТекСтрокаНоменклатура.Цена * НоваяСтрока.Количество;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		СуммаДокумента = Состав.Итог("Сумма");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)  
	
	
	
	Движения.ОстаткиТовара.Записывать = Истина;
	Движения.ТранзитТоваров.Записывать = Истина;
	
	Для Каждого ТекСтрокаСостав Из Состав Цикл
		Если ЗначениеЗаполнено(ТекСтрокаСостав.Номенклатура) Тогда 
			Движение = Движения.ОстаткиТовара.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Номенклатура = ТекСтрокаСостав.Номенклатура;
			Движение.Филиал = ФилиалОтправитель;
			Движение.Количество = ТекСтрокаСостав.Количество;
			Движение.Сумма = ТекСтрокаСостав.Сумма;
			
			Движение = Движения.ТранзитТоваров.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.ФилиалПолучатель = ФилиалПолучатель;
			Движение.Номенклатура = ТекСтрокаСостав.Номенклатура;
			Движение.Количество = ТекСтрокаСостав.Количество;
			
			Если  СтатусПеремещения = Перечисления.СтатусПеремещенияТоваров.Принят Тогда
				
				Движение = Движения.ОстаткиТовара.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.Период = ДатаПрихода;
				Движение.Номенклатура = ТекСтрокаСостав.Номенклатура;
				Движение.Филиал = ФилиалПолучатель;
				Движение.Количество = ТекСтрокаСостав.Количество;
				Движение.Сумма = ТекСтрокаСостав.Сумма;
				
				Движение = Движения.ТранзитТоваров.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Период = ДатаПрихода;
				Движение.ФилиалПолучатель = ФилиалПолучатель;
				Движение.Номенклатура = ТекСтрокаСостав.Номенклатура;
				Движение.Количество = ТекСтрокаСостав.Количество;
				
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если СтатусПеремещения = Перечисления.СтатусПеремещенияТоваров.Принят И НЕ ЗначениеЗаполнено(ДатаПрихода) Тогда
		Сообщить("Укажите дату поступления товара");
		Отказ = Истина
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ФилиалОтправитель) Тогда
		
		ЗапросОстатки = Новый Запрос("ВЫБРАТЬ
		|	ОстаткиТовараОстатки.Номенклатура КАК Номенклатура,
		|	СУММА(ОстаткиТовараОстатки.КоличествоОстаток) КАК Количество
		|ПОМЕСТИТЬ ВТ_Данные
		|ИЗ
		|	РегистрНакопления.ОстаткиТовара.Остатки(, Филиал = &Филиал) КАК ОстаткиТовараОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ОстаткиТовараОстатки.Номенклатура
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОстаткиТовараОбороты.Номенклатура,
		|	ОстаткиТовараОбороты.КоличествоРасход
		|ИЗ
		|	РегистрНакопления.ОстаткиТовара.Обороты(, , Регистратор, Филиал = &Филиал) КАК ОстаткиТовараОбороты
		|ГДЕ
		|	ОстаткиТовараОбороты.Регистратор = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВТ_Данные.Количество) КАК Количество,
		|	ВТ_Данные.Номенклатура КАК Номенклатура
		|ИЗ
		|	ВТ_Данные КАК ВТ_Данные
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Данные.Номенклатура");
		
		ЗапросОстатки.УстановитьПараметр("Филиал", ФилиалОтправитель);
		ЗапросОстатки.УстановитьПараметр("Регистратор", ЭтотОбъект.Ссылка);
		ТаблицаОстатков = ЗапросОстатки.Выполнить().Выгрузить();
		
		Для Каждого Строка Из Состав Цикл
			Если ЗначениеЗаполнено(Строка.Номенклатура) Тогда
				
				НайденнаяСтрока = ТаблицаОстатков.Найти(Строка.Номенклатура,"Номенклатура");
				
				Если НайденнаяСтрока = Неопределено Тогда
					Сообщить("На складе отправителя товара " + Строка.Номенклатура + " нет"); 
					Отказ = Истина                                                           
				Иначе
					Если НайденнаяСтрока.Количество < Строка.Количество Тогда
						Сообщить(СтрШаблон("На складе отправителя не хватает товара %1 ,доступно %2 шт.",Строка.Номенклатура,НайденнаяСтрока.Количество)); 
						Отказ = Истина
					КонецЕсли;
					НайденнаяСтрока.Количество = НайденнаяСтрока.Количество - Строка.Количество;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры
