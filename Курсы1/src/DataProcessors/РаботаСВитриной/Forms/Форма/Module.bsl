
&НаСервере
Процедура ДобавитьТоварНаВитринуНаСервере()

	МЗ = РегистрыСведений.Витрина.СоздатьМенеджерЗаписи();
	МЗ.Товар = Товар;
	МЗ.Филиал = Филиал;
	МЗ.ЕстьНаВитрине = Истина;
	МЗ.ДатаУстановкиНаВитрину = ТекущаяДата();
	МЗ.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьТоварНаВитрину(Команда)
	ДобавитьТоварНаВитринуНаСервере();
КонецПроцедуры

&НаСервере
Процедура СнятьТоварСВитриныНаСервере()
	
	МЗ = РегистрыСведений.Витрина.СоздатьМенеджерЗаписи();
	МЗ.Товар = Товар;
	МЗ.Филиал = Филиал;
	МЗ.Прочитать();
	
	Если МЗ.Выбран() Тогда
		МЗ.ЕстьНаВитрине = Ложь;
		МЗ.Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьТоварСВитрины(Команда)
	СнятьТоварСВитриныНаСервере();
КонецПроцедуры

&НаСервере
Процедура УдалитьЗаписьВитриныНаСервере()
	
	МЗ = РегистрыСведений.Витрина.СоздатьМенеджерЗаписи();
	МЗ.Товар = Товар;
	МЗ.Филиал = Филиал;
	МЗ.Прочитать();
	
	Если МЗ.Выбран() Тогда
		МЗ.Удалить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЗаписьВитрины(Команда)
	УдалитьЗаписьВитриныНаСервере();
КонецПроцедуры

&НаСервере
Процедура ДобавитьТоварЧерезНаборНаСервере()
	
	НЗ = РегистрыСведений.Витрина.СоздатьНаборЗаписей();
	НЗ.Отбор.Товар.Установить(Товар);
	НЗ.Отбор.Филиал.Установить(Филиал);
	
	НоваяЗапись = НЗ.Добавить();
	НоваяЗапись.Товар = Товар;
	НоваяЗапись.Филиал = Филиал;
	НоваяЗапись.ЕстьНаВитрине = Истина;
	НоваяЗапись.ДатаУстановкиНаВитрину = ТекущаяДата();
	
	НЗ.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьТоварЧерезНабор(Команда)
	ДобавитьТоварЧерезНаборНаСервере();
КонецПроцедуры

&НаСервере
Процедура СнятьТоварЧерезНаборНаСервере()
	
	НЗ = РегистрыСведений.Витрина.СоздатьНаборЗаписей();
	НЗ.Отбор.Товар.Установить(Товар);
	НЗ.Отбор.Филиал.Установить(Филиал);
	НЗ.Прочитать();
	
	Для каждого ЗаписьНабор из НЗ Цикл
		ЗаписьНабор.ЕстьНаВитрине = Ложь;
	КонецЦикла;
	
	НЗ.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьТоварЧерезНабор(Команда)
	СнятьТоварЧерезНаборНаСервере();
КонецПроцедуры

&НаСервере
Процедура УдалитьТоварЧерезНаборНаСервере()
	
	НЗ = РегистрыСведений.Витрина.СоздатьНаборЗаписей();
	НЗ.Отбор.Товар.Установить(Товар);
	НЗ.Отбор.Филиал.Установить(Филиал);
	НЗ.Записать();
	
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьТоварЧерезНабор(Команда)
	УдалитьТоварЧерезНаборНаСервере();
КонецПроцедуры
