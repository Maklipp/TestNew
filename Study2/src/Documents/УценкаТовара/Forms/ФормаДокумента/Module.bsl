
&НаСервере
Процедура ПровестиРаспровестиНаСервере()
	
	ОбъектДокумента = РеквизитФормыВЗначение("Объект");
	Если (Объект.Проведен) Тогда
		ОбъектДокумента.Записать(РежимЗаписиДокумента.ОтменаПроведения);
	Иначе
		ОбъектДокумента.Дата = ТекущаяДата();
		ОбъектДокумента.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
	КонецЕсли;
	ЗначениеВРеквизитФормы(ОбъектДокумента, "Объект");
	
	ОбновитьСтатусДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиРаспровести(Команда)
	ПровестиРаспровестиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПометитьСнятьПометкуУдаленияНаСервере()
	
	Если (Объект.Проведен) Тогда
		Сообщить("Запрещено устанавливать пометку на проведенный документ.");
		Возврат;
	КонецЕсли;
	
	ОбъектДокумента = РеквизитФормыВЗначение("Объект");
	ОбъектДокумента.УстановитьПометкуУдаления(НЕ ОбъектДокумента.ПометкаУдаления);
	ЗначениеВРеквизитФормы(ОбъектДокумента, "Объект");
	
	ОбновитьСтатусДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьСнятьПометкуУдаления(Команда)
	ПометитьСнятьПометкуУдаленияНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьСтатусДокумента();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбновитьСтатусДокумента();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьСтатусДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаИзмененияЦеныПриИзменении(Элемент)
	
	Если Объект.ДатаИзмененияЦены < Объект.Дата Тогда
		Объект.ДатаИзмененияЦены = 0;
		Сообщить("Дата изменения цены не может быть меньше даты документа.");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииЦен(Элемент)
	
	ПересчитатьСуммуДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Состав.Итог("НоваяЦена") - Объект.Состав.Итог("Цена");
	
КонецПроцедуры 

&НаСервере
Процедура ОбновитьСтатусДокумента()
	
	Если ЭтаФорма.Параметры.Ключ.Пустая() Тогда
		Элементы.ПризнакПроведения.Заголовок  = "Новый";
		Элементы.ПризнакПроведения.ЦветТекста = WebЦвета.Серый;
	ИначеЕсли Объект.Проведен Тогда 
		Элементы.ПризнакПроведения.Заголовок  = "Проведен";
		Элементы.ПризнакПроведения.ЦветТекста = WebЦвета.ЗеленыйЛес;
	ИначеЕсли НЕ Объект.Проведен И НЕ Объект.ПометкаУдаления Тогда 
		Элементы.ПризнакПроведения.Заголовок  = "Записан";
		Элементы.ПризнакПроведения.ЦветТекста = WebЦвета.СероСиний;
	Иначе
		Элементы.ПризнакПроведения.Заголовок  = "Удален";
		Элементы.ПризнакПроведения.ЦветТекста = WebЦвета.КрасноФиолетовый;
	КонецЕсли;
	
	
КонецПроцедуры 




