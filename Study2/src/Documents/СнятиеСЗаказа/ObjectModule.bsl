
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		
		Автор = ДанныеЗаполнения.Автор;
		Комментарий = ДанныеЗаполнения.Комментарий;
		Контрагент = ДанныеЗаполнения.Контрагент;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		ФилиалПоставки = ДанныеЗаполнения.ФилиалПоставки;
		
		ЗапросТовара = Новый Запрос("ВЫБРАТЬ
		|	ЗаказанныеТоварыОстаткиИОбороты.Номенклатура КАК Номенклатура,
		|	СУММА(ЗаказанныеТоварыОстаткиИОбороты.КоличествоКонечныйОстаток) КАК Количество,
		|	ЗаказанныеТоварыОстаткиИОбороты.СуммаПриход / ЗаказанныеТоварыОстаткиИОбороты.КоличествоПриход КАК Цена,
		|	СУММА(ЗаказанныеТоварыОстаткиИОбороты.СуммаПриход / ЗаказанныеТоварыОстаткиИОбороты.КоличествоПриход * ЗаказанныеТоварыОстаткиИОбороты.КоличествоКонечныйОстаток) КАК Сумма
		|ИЗ
		|	РегистрНакопления.ЗаказанныеТовары.ОстаткиИОбороты(, , , , Заказ = &Заказ) КАК ЗаказанныеТоварыОстаткиИОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказанныеТоварыОстаткиИОбороты.Номенклатура,
		|	ЗаказанныеТоварыОстаткиИОбороты.СуммаПриход / ЗаказанныеТоварыОстаткиИОбороты.КоличествоПриход");
		
		ЗапросТовара.УстановитьПараметр("Заказ", ДанныеЗаполнения.Ссылка);
		ТаблицаТовара = ЗапросТовара.Выполнить().Выгрузить();
		
		Состав.Загрузить(ТаблицаТовара);
		СуммаДокумента = Состав.Итог("Сумма");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ЗаказанныеТовары.Записывать = Истина;
	Для Каждого ТекСтрокаСостав Из Состав Цикл 
		Если ЗначениеЗаполнено(ТекСтрокаСостав.Номенклатура ) Тогда
			Движение = Движения.ЗаказанныеТовары.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Заказ = ДокументОснование;
			Движение.Филиал = ФилиалПоставки;
			Движение.Номенклатура = ТекСтрокаСостав.Номенклатура;
			Движение.Количество = ТекСтрокаСостав.Количество;
			Движение.Сумма = ТекСтрокаСостав.Сумма;
		КонецЕсли;
	КонецЦикла;  
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ЗаказанныеТоварыОстатки.Номенклатура КАК Номенклатура,
	|	ЗаказанныеТоварыОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ ВременнаяТаблица
	|ИЗ
	|	РегистрНакопления.ЗаказанныеТовары.Остатки(, Заказ = &Заказ) КАК ЗаказанныеТоварыОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказанныеТоварыОбороты.Номенклатура,
	|	-ЗаказанныеТоварыОбороты.КоличествоОборот
	|ИЗ
	|	РегистрНакопления.ЗаказанныеТовары.Обороты(, , Регистратор) КАК ЗаказанныеТоварыОбороты
	|ГДЕ
	|	ЗаказанныеТоварыОбороты.Регистратор = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблица.Номенклатура КАК Номенклатура,
	|	СУММА(ВременнаяТаблица.КоличествоОстаток) КАК Количество
	|ИЗ
	|	ВременнаяТаблица КАК ВременнаяТаблица
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблица.Номенклатура");
	
	Запрос.УстановитьПараметр("Заказ", ДокументОснование);
	Запрос.УстановитьПараметр("Регистратор", ЭтотОбъект.Ссылка);
	ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();	
	
	Для Каждого Строка из Состав Цикл 
		Если ЗначениеЗаполнено(Строка.Номенклатура) Тогда
			
			НайденнаяСтрока = ТаблицаЗапроса.Найти(Строка.Номенклатура,"Номенклатура");
			
			Если НайденнаяСтрока = Неопределено Тогда
				Отказ = Истина;
				Сообщить("Товар " + Строка.Номенклатура + " не был в заказе, либо был принят получателем");
			Иначе
				Если НайденнаяСтрока.Количество < Строка.Количество Тогда
					Отказ = Истина;
					Сообщить(СтрШаблон("Вы не можете снять с заказа товар %1, т.к.  не совпадает с количеством заказаного товара", Строка.Номенклатура));
				КонецЕсли;
				
				НайденнаяСтрока.Количество = НайденнаяСтрока.Количество - Строка.Количество;	
			КонецЕсли; 
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
