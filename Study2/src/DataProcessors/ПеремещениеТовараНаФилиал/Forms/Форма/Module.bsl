
&НаСервере
Процедура РасчитатьНаСервере()
	
	Если НЕ ЗначениеЗаполнено(ФилиалОтправитель) ИЛИ НЕ ЗначениеЗаполнено(ФилиалПолучатель) Тогда
		Сообщить("Филиалы не заполнены.");
		Возврат
	КонецЕсли;
	
	ТабЧасть.Очистить();
	
	ЗапросДанных = Новый Запрос("ВЫБРАТЬ
	                            |	Номенклатура.Ссылка КАК Номенклатура
	                            |ПОМЕСТИТЬ ВТ_Номенклатура
	                            |ИЗ
	                            |	Справочник.Номенклатура КАК Номенклатура
	                            |ГДЕ
	                            |	(&НеПроверятьНоменклатуру
	                            |			ИЛИ Номенклатура.Ссылка В (&Номенклатура))
	                            |	И Номенклатура.ЭтоГруппа = ЛОЖЬ
	                            |;
	                            |
	                            |////////////////////////////////////////////////////////////////////////////////
	                            |ВЫБРАТЬ
	                            |	ОстаткиТовараОстатки.Номенклатура КАК Номенклатура,
	                            |	СУММА(ОстаткиТовараОстатки.КоличествоОстаток) КАК КоличествоОстаток
	                            |ПОМЕСТИТЬ ВТ_ТекущийОстатокПолучатель
	                            |ИЗ
	                            |	РегистрНакопления.ОстаткиТовара.Остатки(
	                            |			,
	                            |			Филиал В (&ФилиалПолучатель)
	                            |				И (&НеПроверятьНоменклатуру
	                            |					ИЛИ Номенклатура В (&Номенклатура))) КАК ОстаткиТовараОстатки
	                            |
	                            |СГРУППИРОВАТЬ ПО
	                            |	ОстаткиТовараОстатки.Номенклатура
	                            |;
	                            |
	                            |////////////////////////////////////////////////////////////////////////////////
	                            |ВЫБРАТЬ
	                            |	ОстаткиТовараОстатки.Номенклатура КАК Номенклатура,
	                            |	СУММА(ОстаткиТовараОстатки.КоличествоОстаток) КАК КоличествоОстаток
	                            |ПОМЕСТИТЬ ВТ_ТекущийОстатокОтправитель
	                            |ИЗ
	                            |	РегистрНакопления.ОстаткиТовара.Остатки(
	                            |			,
	                            |			Филиал В (&ФилиалОтправитель)
	                            |				И (&НеПроверятьНоменклатуру
	                            |					ИЛИ Номенклатура В (&Номенклатура))) КАК ОстаткиТовараОстатки
	                            |
	                            |СГРУППИРОВАТЬ ПО
	                            |	ОстаткиТовараОстатки.Номенклатура
	                            |;
	                            |
	                            |////////////////////////////////////////////////////////////////////////////////
	                            |ВЫБРАТЬ
	                            |	ЕСТЬNULL(ВЫБОР
	                            |			КОГДА КлассНоменклатурыСрезПоследних.Класс = ЗНАЧЕНИЕ(Перечисление.КлассНоменклатуры.А)
	                            |				ТОГДА 2
	                            |			КОГДА КлассНоменклатурыСрезПоследних.Класс = ЗНАЧЕНИЕ(Перечисление.КлассНоменклатуры.В)
	                            |				ТОГДА 1.5
	                            |			КОГДА КлассНоменклатурыСрезПоследних.Класс = ЗНАЧЕНИЕ(Перечисление.КлассНоменклатуры.С)
	                            |				ТОГДА 1
	                            |			КОГДА КлассНоменклатурыСрезПоследних.Класс = ЗНАЧЕНИЕ(Перечисление.КлассНоменклатуры.СнятСПродаж)
	                            |				ТОГДА 0
	                            |		КОНЕЦ, 0) КАК Класс,
	                            |	КлассНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура
	                            |ПОМЕСТИТЬ ВТ_Класс
	                            |ИЗ
	                            |	РегистрСведений.КлассНоменклатуры.СрезПоследних(
	                            |			&ПериодКонец,
	                            |			&НеПроверятьНоменклатуру
	                            |				ИЛИ Номенклатура В (&Номенклатура)) КАК КлассНоменклатурыСрезПоследних
	                            |;
	                            |
	                            |////////////////////////////////////////////////////////////////////////////////
	                            |ВЫБРАТЬ
	                            |	ОстаткиТовараОбороты.Номенклатура КАК Номенклатура,
	                            |	СУММА(ОстаткиТовараОбороты.КоличествоРасход) КАК КоличествоРасход
	                            |ПОМЕСТИТЬ ВТ_ПроданоЗаПериод
	                            |ИЗ
	                            |	РегистрНакопления.ОстаткиТовара.Обороты(
	                            |			&ПериодНачало,
	                            |			&ПериодКонец,
	                            |			Запись,
	                            |			Филиал В (&ФилиалПолучатель)
	                            |				И (&НеПроверятьНоменклатуру
	                            |					ИЛИ Номенклатура В (&Номенклатура))) КАК ОстаткиТовараОбороты
	                            |
	                            |СГРУППИРОВАТЬ ПО
	                            |	ОстаткиТовараОбороты.Номенклатура
	                            |;
	                            |
	                            |////////////////////////////////////////////////////////////////////////////////
	                            |ВЫБРАТЬ
	                            |	МинимальныйОстатокТовараНаФилиалеСрезПоследних.Номенклатура КАК Номенклатура,
	                            |	СУММА(МинимальныйОстатокТовараНаФилиалеСрезПоследних.Количество) КАК Количество
	                            |ПОМЕСТИТЬ ВТ_МинимальныйОстатокПолучатель
	                            |ИЗ
	                            |	РегистрСведений.МинимальныйОстатокТовараНаФилиале.СрезПоследних(
	                            |			,
	                            |			Филиал В (&ФилиалПолучатель)
	                            |				И (&НеПроверятьНоменклатуру
	                            |					ИЛИ Номенклатура В (&Номенклатура))) КАК МинимальныйОстатокТовараНаФилиалеСрезПоследних
	                            |
	                            |СГРУППИРОВАТЬ ПО
	                            |	МинимальныйОстатокТовараНаФилиалеСрезПоследних.Номенклатура
	                            |;
	                            |
	                            |////////////////////////////////////////////////////////////////////////////////
	                            |ВЫБРАТЬ
	                            |	МинимальныйОстатокТовараНаФилиалеСрезПоследних.Номенклатура КАК Номенклатура,
	                            |	СУММА(МинимальныйОстатокТовараНаФилиалеСрезПоследних.Количество) КАК Количество
	                            |ПОМЕСТИТЬ ВТ_МинимальныйОстатокОтправитель
	                            |ИЗ
	                            |	РегистрСведений.МинимальныйОстатокТовараНаФилиале.СрезПоследних(
	                            |			,
	                            |			Филиал В (&ФилиалОтправитель)
	                            |				И (&НеПроверятьНоменклатуру
	                            |					ИЛИ Номенклатура В (&Номенклатура))) КАК МинимальныйОстатокТовараНаФилиалеСрезПоследних
	                            |
	                            |СГРУППИРОВАТЬ ПО
	                            |	МинимальныйОстатокТовараНаФилиалеСрезПоследних.Номенклатура
	                            |;
	                            |
	                            |////////////////////////////////////////////////////////////////////////////////
	                            |ВЫБРАТЬ
	                            |	ТранзитТоваровОбороты.Номенклатура КАК Номенклатура,
	                            |	СУММА(ТранзитТоваровОбороты.КоличествоОборот) КАК КоличествоПриход
	                            |ПОМЕСТИТЬ ВТ_ВПути
	                            |ИЗ
	                            |	РегистрНакопления.ТранзитТоваров.Обороты(
	                            |			,
	                            |			,
	                            |			Запись,
	                            |			ФилиалПолучатель = &ФилиалПолучатель
	                            |				И (&НеПроверятьНоменклатуру
	                            |					ИЛИ Номенклатура В (&Номенклатура))) КАК ТранзитТоваровОбороты
	                            |ГДЕ
	                            |	ТранзитТоваровОбороты.Регистратор.СтатусПеремещения = ЗНАЧЕНИЕ(Перечисление.СтатусПеремещенияТоваров.ВПути)
	                            |
	                            |СГРУППИРОВАТЬ ПО
	                            |	ТранзитТоваровОбороты.Номенклатура
	                            |;
	                            |
	                            |////////////////////////////////////////////////////////////////////////////////
	                            |ВЫБРАТЬ
	                            |	ВТ_Номенклатура.Номенклатура КАК Номенклатура,
	                            |	ЕСТЬNULL(ВТ_МинимальныйОстатокОтправитель.Количество, 0) КАК МинимальныйОстатокОтправитель,
	                            |	ЕСТЬNULL(ВТ_МинимальныйОстатокПолучатель.Количество, 0) КАК МинимальныйОстатокПолучатель,
	                            |	ЕСТЬNULL(ВТ_ТекущийОстатокОтправитель.КоличествоОстаток, 0) КАК ТекущийОстатокОтправитель,
	                            |	ЕСТЬNULL(ВТ_ТекущийОстатокПолучатель.КоличествоОстаток, 0) КАК ТекущийОстатокПолучатель,
	                            |	ЕСТЬNULL(ВТ_ВПути.КоличествоПриход, 0) КАК ВПутиПолучатель,
	                            |	ЕСТЬNULL(ВТ_ПроданоЗаПериод.КоличествоРасход, 0) КАК ПроданоПолучатель,
	                            |	ЕСТЬNULL(ВТ_Класс.Класс, 0) КАК Класс
	                            |ИЗ
	                            |	ВТ_Номенклатура КАК ВТ_Номенклатура
	                            |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МинимальныйОстатокОтправитель КАК ВТ_МинимальныйОстатокОтправитель
	                            |		ПО ВТ_Номенклатура.Номенклатура = ВТ_МинимальныйОстатокОтправитель.Номенклатура
	                            |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МинимальныйОстатокПолучатель КАК ВТ_МинимальныйОстатокПолучатель
	                            |		ПО ВТ_Номенклатура.Номенклатура = ВТ_МинимальныйОстатокПолучатель.Номенклатура
	                            |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТекущийОстатокОтправитель КАК ВТ_ТекущийОстатокОтправитель
	                            |		ПО ВТ_Номенклатура.Номенклатура = ВТ_ТекущийОстатокОтправитель.Номенклатура
	                            |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТекущийОстатокПолучатель КАК ВТ_ТекущийОстатокПолучатель
	                            |		ПО ВТ_Номенклатура.Номенклатура = ВТ_ТекущийОстатокПолучатель.Номенклатура
	                            |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВПути КАК ВТ_ВПути
	                            |		ПО ВТ_Номенклатура.Номенклатура = ВТ_ВПути.Номенклатура
	                            |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПроданоЗаПериод КАК ВТ_ПроданоЗаПериод
	                            |		ПО ВТ_Номенклатура.Номенклатура = ВТ_ПроданоЗаПериод.Номенклатура
	                            |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Класс КАК ВТ_Класс
	                            |		ПО ВТ_Номенклатура.Номенклатура = ВТ_Класс.Номенклатура
	                            |
	                            |УПОРЯДОЧИТЬ ПО
	                            |	Номенклатура");
	
	ЗапросДанных.УстановитьПараметр("ПериодНачало",Период.ДатаНачала);
	ЗапросДанных.УстановитьПараметр("ПериодКонец",Период.ДатаОкончания);
	ЗапросДанных.УстановитьПараметр("ФилиалПолучатель",ФилиалПолучатель);
	ЗапросДанных.УстановитьПараметр("ФилиалОтправитель",ФилиалОтправитель);
	ЗапросДанных.УстановитьПараметр("Номенклатура",Номенклатура);
	
	Если Номенклатура.Количество() > 0 Тогда
		ЗапросДанных.УстановитьПараметр("НеПроверятьНоменклатуру",Ложь);
	Иначе
		ЗапросДанных.УстановитьПараметр("НеПроверятьНоменклатуру",Истина);
	КонецЕсли;
	
	Выборка = ЗапросДанных.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ТабЧасть.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Выборка); 
		
		ОтправительМожетОтправить = Выборка.ТекущийОстатокОтправитель - Выборка.МинимальныйОстатокОтправитель;
		Если ОтправительМожетОтправить <= 0 Тогда
			НоваяСтрока.КПеремещению = 0;
		Иначе
			КПеремещениюПредварительноеКоличество = Выборка.ПроданоПолучатель - Выборка.ТекущийОстатокПолучатель;
			Если КПеремещениюПредварительноеКоличество < Выборка.МинимальныйОстатокПолучатель Тогда
				КПеремещениюПредварительноеКоличество = Выборка.МинимальныйОстатокПолучатель
			КонецЕсли;
			КПеремещениюПредварительноеКоличество = КПеремещениюПредварительноеКоличество * Выборка.Класс;
			КПеремещениюПредварительноеКоличество = КПеремещениюПредварительноеКоличество - Выборка.ВПутиПолучатель;
			
			Если ОтправительМожетОтправить > КПеремещениюПредварительноеКоличество Тогда
				НоваяСтрока.КПеремещению = КПеремещениюПредварительноеКоличество;
			Иначе
				НоваяСтрока.КПеремещению = ОтправительМожетОтправить;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Расчитать(Команда)
	РасчитатьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументПеремещения(Команда)
	
	ПередаваемыеДанные = Новый Структура("ЗаполнениеИзОбработки",ИСТИНА);
	ПередаваемыеДанные.Вставить("ФилиалПолучатель",ФилиалПолучатель);
	ПередаваемыеДанные.Вставить("ФилиалОтправитель",ФилиалОтправитель);
	
	Адрес = ПоместитьВоВременноеХранилищеТабЧасть();
	ПередаваемыеДанные.Вставить("Адрес",Адрес);
	
	ОткрытьФорму("Документ.ПеремещениеТовара.Форма.ФормаДокумента",ПередаваемыеДанные,ЭтотОбъект);
	
КонецПроцедуры  

&НаСервере
Функция ПоместитьВоВременноеХранилищеТабЧасть()
	
	ТаблицаДляПередачиДанных = Новый ТаблицаЗначений;
	ТаблицаДляПередачиДанных.Колонки.Добавить("Номенклатура");
	ТаблицаДляПередачиДанных.Колонки.Добавить("Количество");
	
	Для Каждого Строка Из ТабЧасть Цикл
		Если Строка.КПеремещению > 0 Тогда
			НоваяСтрока = ТаблицаДляПередачиДанных.Добавить();
			НоваяСтрока.Номенклатура = Строка.Номенклатура;
			НоваяСтрока.Количество = Строка.КПеремещению;
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаДанныхПеремещениеТовара = Новый УникальныйИдентификатор;
	Адрес = ПоместитьВоВременноеХранилище(ТаблицаДляПередачиДанных, ТаблицаДанныхПеремещениеТовара);
	Возврат Адрес
	
КонецФункции

